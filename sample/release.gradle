final DIR = 'sample/signing'

android {
    signingConfigs {
        debug {
            storeFile file('signing/debug.jks')
        }
        upload {
            storeFile file('signing/upload.jks')
            storePassword System.env.UPLOAD_KEYSTORE_PWD ?: ''
            keyAlias 'upload'
            keyPassword System.env.UPLOAD_KEYSTORE_PWD ?: ''
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.upload
        }
    }
}

play {
    serviceAccountCredentials.set(file('signing/play_account.json'))
    track.set('alpha')
}

project.afterEvaluate {
    // Setting up 'publishSample' task
    // Note, that in order to work this task requires ENCRYPT_KEY and UPLOAD_KEYSTORE_PWD env vars

    final publish = 'publishSample'

    project.task publish
    project.tasks[publish].dependsOn 'decryptUploadKeystore'
    project.tasks[publish].dependsOn 'decryptPlayAccount'
    project.tasks[publish].dependsOn 'publishReleaseBundle'
    project.tasks[publish].finalizedBy 'cleanUploadKeystore'
    project.tasks[publish].finalizedBy 'cleanPlayAccount'
}

task decryptUploadKeystore {
    doFirst {
        println 'Decrypt upload keystore'
        def proc = ("openssl aes-256-cbc -d" +
                " -in $DIR/upload.jks.enc" +
                " -out $DIR/upload.jks" +
                " -k $System.env.ENCRYPT_KEY").execute()
        println "Error?:\n" + proc.err.text
    }
}

task cleanUploadKeystore {
    doLast {
        println 'Clean up upload keystore'
        "rm $DIR/upload.jks".execute()
    }
}

task decryptPlayAccount {
    doFirst {
        println 'Decrypt Play account'
        def proc = ("openssl aes-256-cbc -d" +
                " -in $DIR/play_account.json.enc" +
                " -out $DIR/play_account.json" +
                " -k $System.env.ENCRYPT_KEY").execute()
        println "Error?:\n" + proc.err.text
    }
}

task cleanPlayAccount {
    doLast {
        println 'Clean up Play account'
        "rm $DIR/play_account.json".execute()
    }
}
